

      subroutine detmat (n, a, ipiv, adet, pdet)

      implicit none


C +------------------------------------------------------------+
C |  detmat   --   CAJH, 07.2011                               |
C |                                                            |
C |                                    (based on PHFB detmat)  |
C |                                                            |
C |                                                            |
C |  Compute the absolute value and the phase of the           |
C |  determinant of a SQUARE, COMPLEX matrix A (dimension      |
C |  n x n), from its LU decomposition.                        |
C |                                                            |
C |  In particular, detmat uses the LU decomposition with      |
C |  pivoting generated by the LaPack routine zgetrf. The      |
C |  LU decomposition is returned by LaPack in a single        |
C |  matrix, where the lower triangle corresponds to L and     |
C |  the upper triangle (including the diagonal) corresponds   |
C |  to U. Note that the diagonal unit elements corresponding  |
C |  to L are not stored anywhere.                             |
C |                                                            |
C |  The LU decomposition generated by LaPack then reads       |
C |    A = P . L . U,                                          |
C |                                                            |
C |  where P is the permutation matrix (can be recovered       |
C |  from the pivoting indices), and L and U are the lower     |
C |  and upper triangular matrices.                            |
C |                                                            |
C |  The determinant of the matrix A can then be easily        |
C |  computed as                                               |
C |                                                            |
C |    det(A) = det(P) . det(L) . det(U)                       |
C |                                                            |
C |  det(L) and det(U) are easily computed since they are      |
C |  triangular matrices. The determinant of the triangular    |
C |  matrix T is given by                                      |
C |                                                            |
C |    det (T) = prod_{i=1,n}  T(i,i)                          |
C |                                                            |
C |  Hence, det(L) = 1 (all diagonal elements are 1), and      |
C |  needs not be computed. Additionally, det(P) = +/- 1,      |
C |  depending on the signature of the permutation, due to     |
C |  the fact that it is a permutation matrix.                 |
C |                                                            |
C |  Therefore, detmat computes the determinant of A as        |
C |                                                            |
C |    det(A) = +/- 1 * det(U)                                 |
C |                                                            |
C +------------------------------------------------------------+


C     input / output variables

C       n    - leading dimension of matrix a
C       a    - LU decomposition of matrix a (generated by LaPack)
C       ipiv - vector of pivoting indices (as generated by LaPack)
C       adet - absolute value of determinant [ out ]
C       pdet - phase of determinant [ out ]

      real*8      adet, pdet
      integer     n, ipiv(*)
      complex*16  a(n,*)

C     other variables

      integer     i, sgn
      real*8      pi, xr, xi
      real*8      sphs, sabs, phsx, absx, logx


      pi = 4.0d0 * atan (1.0d0)


C     Determine the overall sign of the determinant from the permutation
C     matrix.

      sgn = 1

      do 10 i = 1, n
        if ( ipiv(i) .ne. i ) sgn = -sgn
 10   continue


C     Initialize values of phase and the product of absolute values.

      sphs = 0.0d0
      sabs = 0.0d0

C     Add pi to the phase if the determinant has an overall negative
C     sign.

      if ( sgn .eq. -1 ) sphs = sphs + pi


C     Loop over diagonal elements of the upper matrix.

      do 20 i = 1, n

C     Recover real and imaginary part of a(i,i).

        xr = dble (a(i,i))
        xi = aimag (a(i,i))

C     Compute phase and absolute value.

        phsx = atan2 (xi, xr)
        absx = sqrt (xr**2 + xi**2)
        logx = log (absx)

C     Add to overall quantities.

        sphs = sphs + phsx
        sabs = sabs + logx
 20   continue


C     Prepare quantities to return:
C       adet - absolute value of the determinant
C       pdet - phase of the determinant

      adet = exp (sabs)
      pdet = sphs


      return
      end


